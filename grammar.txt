Program     -> Declaration*

Declaration -> VarDecl
            | FunctionDecl
            | Statement

Type        -> "int" | "float" | "bool" | "string" | "void"

VarDecl     -> Type ID ";"

FunctionDecl -> "function" Type ID "(" Params? ")" Block

Params      -> Param ("," Param)*
Param       -> Type ID

Statement   -> IfStmt | WhileStmt | ForStmt | ReturnStmt | PrintStmt | Block | Assign | Call

IfStmt      -> "if" "(" Expr ")" Statement ("else" IfStmt | "else" Statement)?
WhileStmt   -> "while" "(" Expr ")" Statement
ForStmt     -> "for" "(" ForInit ";" Expr? ";" Expr? ")" Statement
ForInit     -> VarDeclNoSemi | AssignNoSemi | Îµ

ReturnStmt  -> "return" Expr? ";"
PrintStmt   -> "print" "(" Expr ")" ";"
Block       -> "{" Declaration* "}"

Assign      -> ID "=" Expr ";"
AssignNoSemi -> ID "=" Expr
Call        -> ID "(" (Expr ("," Expr)*)? ")" ";"

Expr        -> Or
Or          -> And ("||" And)*
And         -> Equality ("&&" Equality)*
Equality    -> Comparison (("==" | "!=") Comparison)*
Comparison  -> Term (("<" | "<=" | ">" | ">=") Term)*
Term        -> Factor (("+" | "-") Factor)*
Factor      -> Unary (("*" | "/") Unary)*
Unary       -> ("-" | "!") Unary | Primary
Primary     -> NUMBER | FLOAT | STRING | "true" | "false" | ID | "(" Expr ")"